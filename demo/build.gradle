plugins {
    id 'com.android.application'
}

android {
    namespace 'com.liulishuo.filedownloader.demo'

    defaultConfig {
        minSdk 21

        File file = project.rootProject.file('local.properties');
        def needLog = null
        if (file.exists()) {
            Properties p = new Properties()
            p.load(file.newDataInputStream())
            needLog = p.getProperty("needLog")
        }
        buildConfigField "boolean", "DOWNLOAD_NEED_LOG", needLog == "true" ? "true" : "false"
    }

    buildTypes {
        all {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    lintOptions {
        // This seems to be firing due to okio referencing java.nio.File
        // which is harmless for us.
        warning 'InvalidPackage'
    }
}

dependencies {
    implementation project(':library')

    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.12.0'
    debugImplementation 'cn.dreamtobe.threaddebugger:threaddebugger:1.6.3'
    releaseImplementation 'cn.dreamtobe.threaddebugger:threaddebugger-no-op:1.6.3'
    // for testing
    implementation 'com.squareup.okio:okio:3.9.0'
}
